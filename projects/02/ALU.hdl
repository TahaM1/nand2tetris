// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:

    // x manipluation
    Mux16(a=x, b=zerox, sel=zx, out=zxout);
    Mux16(a=zxout, b=notx, sel=nx, out=nxout);
    // zx = 1, x = 0
    And16(a=x, b=false, out=zerox);
    // x = !x
    Not16(in=zxout, out=notx);


    // y manipluation
    Mux16(a=y, b=zeroy, sel=zy, out=zyout);
    Mux16(a=zyout, b=noty, sel=ny, out=nyout);
    // y = 0
    And16(a=y, b=false, out=zeroy);
    // !y
    Not16(in=zyout, out=noty);


    // ADDer
    Add16(a=nxout, b=nyout, out=addout);
    // ANDer
    And16(a=nxout, b=nyout, out=andout);
    // Routing output based on f val selected
    Mux16(a=andout, b=addout, sel=f, out=routedaddout);

    // negate output
    Not16(in=routedaddout, out=negatedaddout);
    // also routes negative bit to ng (out[15]=ng)
    Mux16(a=routedaddout, b=negatedaddout, sel=no, out[15]=ng, out[0..7]=half1out, out[8..15]=half2out, out=out);

    // setting zr is output is zero
    Or8Way(in=half1out, out=zror1);
    Or8Way(in=half2out, out=zror2);
    Or(a=zror1, b=zror2, out=zrorcombined);
    Not(in=zrorcombined, out=zr);
    
    



}